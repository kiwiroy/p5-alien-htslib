# -*- mode: perl; -*-
use strict;
use warnings;
use alienfile;
use Path::Tiny qw{path};

our $htslib_version = '1.8';

plugin 'PkgConfig' => (
  pkg_name => 'htslib',
);

share {
  requires 'Alien::curl' => '0.06';
  requires 'Alien::Libbz2' => '0.22';

  start_url "https://github.com/samtools/htslib/releases/download/${htslib_version}/htslib-${htslib_version}.tar.bz2";

  plugin Download => ( version => qr/([0-9\.]+)/, bootstrap_ssl => 1 );

  plugin Extract => ( format => 'tar.bz2' );

  plugin 'Build::Autoconf' => ( with_pic => 0 ); ## --with-pic creates configure warning.

  build [
    '%{configure} --enable-s3 --enable-libcurl --enable-gcs CFLAGS=-fpic',
    '%{make}',
    '%{make} install'
  ];

  after gather => sub {
    my ($build) = @_;

    my ($stage) = path('.')->absolute->child('lib');
    my @dynamic;
    ## we use the static library only, remove any dynamic libs
    push @dynamic, glob "$stage/*.dylib";
    push @dynamic, glob "$stage/*.so";
    push @dynamic, glob "$stage/*.so.*";
    push @dynamic, glob "$stage/*.dll";
    unlink @dynamic if @dynamic;

    for (qw{libs libs_static}) {
        $build->runtime_prop->{$_} =
            join ' ', $build->runtime_prop->{$_}, '-lcurl'
            unless $build->runtime_prop->{$_} =~ m/\-lcurl/;
    }

  };

  test [ '%{make} check' ] unless $ENV{ALIEN_HTSLIB_NO_CHECK};

};
